def VERSIONS = [
	'javax.servlet:javax.servlet-api:latest.release',
	'jakarta.platform:jakarta.jakartaee-web-api:9.+',

	'io.micrometer:context-propagation:1.0.+',

	'aopalliance:aopalliance:1.0',
	'org.aspectj:aspectjweaver:1.8.+',
	'org.springframework:spring-context:5.+',
	'org.springframework:spring-core:5+',

	// logging
	'ch.qos.logback:logback-classic:1.2.+',
	'org.apache.logging.log4j:log4j-core:2.+',
	'org.slf4j:slf4j-api:1.7.+',
	// otel
	'io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-semconv:1.24.+',
	// zipkin
	'io.zipkin.aws:brave-propagation-aws:latest.release',
	// wavefront
	'com.wavefront:wavefront-sdk-java:3.0.+',
	'com.wavefront:wavefront-internal-reporter-java:1.7.+',
	// test
	'org.assertj:assertj-core:latest.release',
	'org.awaitility:awaitility:latest.release',
	'org.mockito:mockito-core:4.+',
	'org.mockito:mockito-inline:4.+',
	'org.mockito:mockito-junit-jupiter:4.+',
	'com.github.tomakehurst:wiremock-jre8-standalone:latest.release',
	'org.testcontainers:testcontainers:latest.release',
	'org.testcontainers:junit-jupiter:latest.release'
]

def MICROMETER_PLATFORM_VERSIONS = [
	'io.micrometer:micrometer-bom:1.11.0'
]

def PLATFORM_VERSIONS = [
	'io.zipkin.brave:brave-bom:5.14.+',
	// opentelemetry-instrumentation-api dependency above with this
	'io.opentelemetry:opentelemetry-bom:1.25.+',
	'io.opentelemetry:opentelemetry-bom-alpha:1.25.+',
	'io.projectreactor:reactor-bom:2022.0.+', // latest release for tests
	'org.junit:junit-bom:5.9.+'
]

subprojects {
	plugins.withId('java-library') {
		dependencies {
			constraints {
				// Direct dependencies
				VERSIONS.each { version ->
					// java-library plugin has three root configurations, so we apply constraints too all of
					// them so they all can use the managed versions.
					api version
					compileOnly version
					runtimeOnly version
				}
			}
			(MICROMETER_PLATFORM_VERSIONS + PLATFORM_VERSIONS).each { bom ->
				api platform(bom)
				compileOnly platform(bom)
				runtimeOnly platform(bom)
			}
		}
	}
	plugins.withId('java-platform') {
		dependencies {
			constraints {
				rootProject.subprojects
					.findAll { !it.name.contains('micrometer-tracing-bom') }
					.each { api(group: it.group, name: it.name, version: it.version.toString()) }
			}
			javaPlatform.allowDependencies()
			MICROMETER_PLATFORM_VERSIONS.each { api platform(it) }
		}
	}
}

def VERSIONS = [
		'javax.servlet:javax.servlet-api:4.0.+',
		'jakarta.platform:jakarta.jakartaee-web-api:9.1.+',

		'aopalliance:aopalliance:1.0',
		// logging
		'ch.qos.logback:logback-classic:1.2.+',
		'org.apache.logging.log4j:log4j-core:2.+',
		'org.slf4j:slf4j-api:1.7.+',
		// otel
		'io.opentelemetry.instrumentation:opentelemetry-instrumentation-api-semconv:1.+',
		// zipkin
		'io.zipkin.aws:brave-propagation-aws:latest.release',
		// wavefront
		'com.wavefront:wavefront-sdk-java:3.0.+',
		'com.wavefront:wavefront-internal-reporter-java:1.7.+',
		// instrumentations
		'org.mongodb:mongodb-driver-sync:latest.release',
		// test
		'org.assertj:assertj-core:latest.release',
		'org.awaitility:awaitility:latest.release',
		'org.mockito:mockito-core:latest.release',
		'org.mockito:mockito-inline:latest.release',
		'com.github.tomakehurst:wiremock-jre8-standalone:latest.release',
		'org.testcontainers:testcontainers:latest.release',
		'org.testcontainers:junit-jupiter:latest.release'
]

def PLATFORM_VERSIONS = [
		'io.micrometer:micrometer-bom:1.10.0-SNAPSHOT',
		'io.zipkin.brave:brave-bom:5.13.+',
		// opentelemetry-instrumentation-api dependency above with this
		'io.opentelemetry:opentelemetry-bom:1.+',
		'io.opentelemetry:opentelemetry-bom-alpha:1.+',
		'org.junit:junit-bom:5.8.2'
]

subprojects {
	plugins.withId('java-library') {
		dependencies {
			constraints {
				// Direct dependencies
				VERSIONS.each { version ->
					// java-library plugin has three root configurations, so we apply constraints too all of
					// them so they all can use the managed versions.
					api version
					compileOnly version
					runtimeOnly version
				}
			}
			PLATFORM_VERSIONS.each {
				implementation platform(it)
			}
		}
	}
	plugins.withId('java-platform') {
		dependencies {
			constraints {
				rootProject.subprojects.findAll {
					!it.name.contains('micrometer-tracing-bom')
				}.each {
					api(group: it.group,
							name: it.name,
							version: it.version.toString())
				}
			}
			javaPlatform.allowDependencies()
			PLATFORM_VERSIONS.each {
				api platform(it)
			}
		}
	}
}

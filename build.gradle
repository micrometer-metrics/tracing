buildscript {
	ext.gradleJvmVersion = JavaLanguageVersion.of(JavaVersion.current().getMajorVersion())
	ext.javaLanguageVersion = project.hasProperty('toolchainVersion') && project.property('toolchainVersion') != '' ? JavaLanguageVersion.of(project.property('toolchainVersion') as int) : gradleJvmVersion
	ext.javaTargetVersion = JavaVersion.VERSION_1_8

	repositories {
		mavenCentral()
		gradlePluginPortal()
	}

	dependencies {
		classpath libs.plugin.errorprone
		classpath libs.plugin.license
		classpath libs.plugin.nebulaRelease
		classpath libs.plugin.nebulaPublishing
		classpath libs.plugin.nebulaProject
		classpath libs.plugin.nebulaInfo
		classpath libs.plugin.noHttp
		classpath libs.plugin.nexusPublish
		classpath libs.plugin.javaformat
		classpath libs.plugin.jmh

		constraints {
			classpath(libs.asmForPlugins) {
				because 'Supports modern JDKs'
			}
		}
	}

	configurations.classpath.resolutionStrategy.cacheDynamicVersionsFor 0, 'minutes'
}

// Hacks because of Antora's clone/checkout/worktrees behavior
// Antora uses shallow-clone and worktrees to check out branches/tags.
if (project.hasProperty('antora')) {
	'git fetch --unshallow --all --tags'.execute().text // Antora shallow-clones so there is no history (we need commit history to find the last tag in the tree)
	String ref = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
	if (ref == 'HEAD') { // if Antora checks out a tag instead of a branch
		String tag = 'git tag --points-at HEAD'.execute().text.trim() // jgit is not able to figure out tags in Antora's worktree
		if (tag) {
			println "Found release tag: $tag, using it as release.version"
			ext['release.version'] = tag.substring(1)
		}
	}
}

// TODO: remove this hack, see: https://github.com/nebula-plugins/nebula-release-plugin/issues/213
def releaseStage = findProperty('release.stage')
apply plugin: 'com.netflix.nebula.release'
release.defaultVersionStrategy = nebula.plugin.release.git.opinion.Strategies.SNAPSHOT

apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'com.netflix.nebula.info'
apply plugin: 'com.netflix.nebula.project'

allprojects {
	group = 'io.micrometer'
	ext.'release.stage' = releaseStage ?: 'SNAPSHOT'

	afterEvaluate { project -> println "I'm configuring $project.name with version $project.version" }
}

subprojects {
	apply plugin: 'net.ltgt.errorprone'
	apply plugin: 'signing'
	apply plugin: 'io.spring.javaformat'

	if (project.name != 'micrometer-tracing-bom') {
		tasks.withType(JavaCompile).configureEach {
			options.errorprone {
				disableWarningsInGeneratedCode = true
				excludedPaths = ".*/generated/.*"

				disable(
						"StringConcatToTextBlock" // Requires JDK 15+
				)

				error("NullAway")

				// useful for giving NullAway info it can't derive from the program logic
				option("NullAway:AssertsEnabled", "true")
				option("NullAway:OnlyNullMarked", "true")
				option("NullAway:CustomContractAnnotations", "io.micrometer.common.lang.internal.Contract,org.assertj.core.internal.annotation.Contract")
				option("NullAway:CheckContracts", "true")
				option("NullAway:HandleTestAssertionLibraries", "true")
				option("NullAway:ExcludedFieldAnnotations", "org.mockito.Mock,org.mockito.InjectMocks")
				if (javaLanguageVersion.canCompileOrRun(22)) {
					// see https://bugs.openjdk.org/browse/JDK-8346471
					// see https://github.com/uber/NullAway/wiki/JSpecify-Support
					option("NullAway:JSpecifyMode", "true")
				}
				if (!javaLanguageVersion.canCompileOrRun(21)) {
					// Error Prone 2.43.0 requires JDK 21+
					enabled = false
				}
				if (System.env.CI != null) {
					disableAllWarnings = true
				}
			}
		}
		if (project.name.contains('benchmarks')) {
			apply plugin: 'java'
		} else {
			apply plugin: 'java-library'
			dependencies {
				api(libs.jspecify)

				testImplementation platform(libs.junitBom)
				testImplementation libs.junitJupiter
				testRuntimeOnly libs.junitPlatformLauncher
			}
		}
		apply plugin: 'com.github.hierynomus.license'
		apply plugin: 'checkstyle'
		apply plugin: 'io.spring.nohttp'
		apply plugin: 'me.champeau.jmh'

		java {
			// It is more idiomatic to define different features for different sets of optional
			// dependencies, e.g., 'dropwizard' and 'reactor'. If this library published Gradle
			// metadata, Gradle users would be able to use these feature names in their dependency
			// declarations instead of understanding the actual required optional dependencies.
			// But we don't publish Gradle metadata yet and this may be overkill so just have a
			// single feature for now to correspond to any optional dependency.
			registerFeature('optional') {
				usingSourceSet(sourceSets.main)
			}

			toolchain {
				languageVersion = javaLanguageVersion
			}
			sourceCompatibility = javaTargetVersion
			targetCompatibility = javaTargetVersion
		}

		// All projects use optional annotations, but since we don't expose them downstream we would
		// have to add the dependency in every project, which is tedious so just do it here.
		dependencies {
			checkstyle libs.spring.javaformatCheckstyle
			if (javaLanguageVersion.canCompileOrRun(21)) {
				// Error Prone 2.43.0 requires JDK 21+
				errorprone(libs.errorprone)
				errorprone(libs.nullAway)
			}
			jmh libs.jmh
			jmh libs.jmhGeneratorAnnProcess
			jmhAnnotationProcessor libs.jmhGeneratorAnnProcess
			jmh 'net.sf.jopt-simple:jopt-simple'
		}

		jmh {
			duplicateClassesStrategy = DuplicatesStrategy.EXCLUDE
		}

		tasks {
			compileJava {
				options.encoding = 'UTF-8'
				options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'

				sourceCompatibility = javaTargetVersion
				targetCompatibility = javaTargetVersion

				// ensure Java 8 baseline is enforced for main source
				options.release = 8

				doLast {
					task -> logger.info("Compiling with " + task.javaCompiler.get().executablePath)
				}
			}
			compileTestJava {
				options.encoding = 'UTF-8'
				options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'

				sourceCompatibility = JavaVersion.VERSION_11
				targetCompatibility = JavaVersion.VERSION_11
			}

			javadoc {
				configure(options) {
					tags(
							'apiNote:a:API Note:',
							'implSpec:a:Implementation Requirements:',
							'implNote:a:Implementation Note:'
					)
					options.addBooleanOption('Xdoclint:all,-missing', true)
				}
			}
		}

		normalization {
			runtimeClasspath {
				metaInf {
					[
							'Build-Date',
							'Build-Date-UTC',
							'Built-By',
							'Built-OS',
							'Build-Host',
							'Build-Job',
							'Build-Number',
							'Build-Id',
							'Change',
							'Full-Change',
							'Branch',
							'Module-Origin',
							'Created-By',
							'Build-Java-Version'
					].each {
						ignoreAttribute it
						ignoreProperty it
					}
				}
			}
		}

		//noinspection GroovyAssignabilityCheck
		test {
			// set heap size for the test JVM(s)
			maxHeapSize = '1500m'

			useJUnitPlatform {
				excludeTags 'docker'
			}
		}

		tasks.register('dockerTest', Test) {
			// set heap size for the test JVM(s)
			maxHeapSize = '1500m'

			useJUnitPlatform {
				includeTags 'docker'
			}
		}

		tasks.withType(Test).configureEach {
			testLogging.exceptionFormat = 'full'
			develocity.testRetry {
				maxFailures = 5
				maxRetries = 3
			}
		}

		license {
			header rootProject.file('gradle/licenseHeader.txt')
			strictCheck true
			mapping {
				java = 'SLASHSTAR_STYLE'
			}
			sourceSets = project.sourceSets

			ext.year = Calendar.getInstance().get(Calendar.YEAR)
			skipExistingHeaders = true
			exclude '**/*.json' // comments not supported
		}

		// Publish resolved versions.
		plugins.withId('maven-publish') {
			publishing {
				publications {
					nebula(MavenPublication) {
						versionMapping {
							allVariants {
								fromResolutionResult()
							}
						}

						// We publish resolved versions so don't need to publish our dependencyManagement
						// too. This is different from many Maven projects, where published artifacts often
						// don't include resolved versions and have a parent POM including dependencyManagement.
						pom.withXml {
							def dependencyManagement = asNode().get('dependencyManagement')
							if (dependencyManagement) {
								asNode().remove(dependencyManagement)
							}
						}
					}
				}
			}
		}
	}

	plugins.withId('maven-publish') {
		publishing {
			publications {
				nebula(MavenPublication) {
					// Nebula converts dynamic versions to static ones so it's ok.
					suppressAllPomMetadataWarnings()
				}
			}
			repositories {
				maven {
					name = 'Snapshot'
					url = 'https://repo.spring.io/snapshot'
					credentials {
						username findProperty('SNAPSHOT_REPO_USER')
						password findProperty('SNAPSHOT_REPO_PASSWORD')
					}
				}
			}
		}

		signing {
			required = System.env.CIRCLE_STAGE == 'deploy'
			useInMemoryPgpKeys(findProperty('SIGNING_KEY'), findProperty('SIGNING_PASSWORD'))
			sign publishing.publications.nebula
		}

		// Nebula doesn't interface with Gradle's module format so just disable it for now.
		tasks.withType(GenerateModuleMetadata) {
			enabled = false
		}
	}

	tasks.register('downloadDependencies') {
		outputs.upToDateWhen { false }
		doLast {
			project.configurations.findAll { it.canBeResolved }*.files
		}
	}

	if (!['benchmarks'].find { project.name.contains(it) }) {
		apply plugin: 'com.netflix.nebula.maven-publish'
		apply plugin: 'com.netflix.nebula.maven-manifest'
		apply plugin: 'com.netflix.nebula.maven-developer'
		apply plugin: 'com.netflix.nebula.javadoc-jar'
		apply plugin: 'com.netflix.nebula.source-jar'
		apply plugin: 'com.netflix.nebula.maven-apache-license'
		apply plugin: 'com.netflix.nebula.publish-verification'
		apply plugin: 'com.netflix.nebula.contacts'
		apply plugin: 'com.netflix.nebula.info'
		apply plugin: 'com.netflix.nebula.project'

		if (project.name != 'micrometer-tracing-bom') {
			jar {
				manifest.attributes.put('Automatic-Module-Name', project.name.replace('-', '.'))
				metaInf {
					from "$rootDir/LICENSE"
					from "$rootDir/NOTICE"
				}
			}
		}

		contacts {
			'tludwig@vmware.com' {
				moniker 'Tommy Ludwig'
				github 'shakuzen'
			}
			'jivanov@vmware.com' {
				moniker 'Jonatan Ivanov'
				github 'jonatan-ivanov'
			}
			'mgrzejszczak@vmware.com' {
				moniker 'Marcin Grzejszczak'
				github 'marcingrzejszczak'
			}
		}
	}

	description = 'Facade over tracing concepts'

	repositories {
		mavenCentral()
	}

	def check = tasks.findByName('check')
	if (check) project.rootProject.tasks.releaseCheck.dependsOn check
}

nexusPublishing {
	repositories {
		mavenCentral {
			nexusUrl.set(uri('https://ossrh-staging-api.central.sonatype.com/service/local/'))
			snapshotRepositoryUrl.set(uri('https://repo.spring.io/snapshot/')) // not used but necessary for the plugin
			username = findProperty('MAVEN_CENTRAL_USER')
			password = findProperty('MAVEN_CENTRAL_PASSWORD')
		}
	}
}

wrapper {
	gradleVersion = '9.2.1'
}

defaultTasks 'build'

buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}

	dependencies {
		classpath libs.plugin.spring.antora
		classpath libs.plugin.antora
	}
}

apply plugin: 'java'
apply plugin: 'io.spring.antora.generate-antora-yml'
apply plugin: 'org.antora'

description = 'Micrometer Tracing Docs'

repositories {
	mavenCentral()
}

dependencies {
	implementation project(':micrometer-tracing')
	implementation libs.logback
	implementation libs.slf4j

	testImplementation 'io.zipkin.reporter2:zipkin-sender-urlconnection'

	testImplementation project(':micrometer-tracing-test')
	testImplementation project(':micrometer-tracing-integration-test')
	testImplementation libs.aspectjweaver
	testImplementation libs.junitJupiter
	testRuntimeOnly libs.junitPlatformLauncher
	testImplementation libs.mockitoCore
	testImplementation libs.assertj
	testImplementation libs.springContext
	testImplementation platform(libs.reactorBom)
	testImplementation 'io.projectreactor:reactor-core'
	testImplementation 'io.projectreactor:reactor-core-micrometer'
}

antora {
	version = '3.2.0-alpha.2'
	playbook = 'antora-playbook.yml'
	options = ['--clean', '--stacktrace']
	environment = [
		'ALGOLIA_API_KEY': 'cbcaa86034c1961b2c0c73bd0c274862',
		'ALGOLIA_APP_ID': 'R3TXPRVDPR',
		'ALGOLIA_INDEX_NAME': 'micrometer'
	]

	dependencies = [
		'@antora/atlas-extension': '1.0.0-alpha.1',
		'@antora/collector-extension': '1.0.0-alpha.3',
		'@asciidoctor/tabs': '1.0.0-beta.3',
		'@springio/antora-extensions': '1.4.2',
		'@springio/asciidoctor-extensions': '1.0.0-alpha.8',
	]
}

tasks.named('generateAntoraYml') {
	asciidocAttributes = project.provider {
		return ['micrometer-tracing-version': project.version.toString()]
	}
}

tasks.register('generateAntoraResources') {
	dependsOn 'generateAntoraYml'
}

tasks.named('antora') {
	dependsOn 'generateAntoraResources', 'test'
	inputs.files('modules')
}

nohttp {
	source.exclude "modules/ROOT/**/build/**"
}

jar {
	enabled = false
}

javadoc {
	enabled = false
}

tasks.withType(AbstractPublishToMaven).configureEach {
	enabled = false
}

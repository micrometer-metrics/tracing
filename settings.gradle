pluginManagement {
	repositories {
		gradlePluginPortal()
	}
}

plugins {
	id 'com.gradle.develocity' version '3.18.1'
	id 'io.spring.develocity.conventions' version '0.0.21'
	id 'org.gradle.toolchains.foojay-resolver-convention' version '0.8.0'
}

rootProject.name = 'tracing'

develocity {
	server = 'https://ge.micrometer.io'
}

buildCache {
	remote(develocity.buildCache) {
		server = 'https://ge.micrometer.io'
	}
}

include 'micrometer-tracing', 'micrometer-tracing-bom', 'benchmarks'

['brave', 'otel'].each { bridge ->
	include "micrometer-tracing-bridge-$bridge"
	project(":micrometer-tracing-bridge-$bridge").projectDir = new File(rootProject.projectDir, "micrometer-tracing-bridges/micrometer-tracing-bridge-$bridge")
}

['wavefront'].each { reporter ->
	include "micrometer-tracing-reporter-$reporter"
	project(":micrometer-tracing-reporter-$reporter").projectDir = new File(rootProject.projectDir, "micrometer-tracing-reporters/micrometer-tracing-reporter-$reporter")
}

['test', 'integration-test'].each { testProject ->
	include "micrometer-tracing-$testProject"
	project(":micrometer-tracing-$testProject").projectDir = new File(rootProject.projectDir, "micrometer-tracing-tests/micrometer-tracing-$testProject")
}

include 'docs'
